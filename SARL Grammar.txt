		// Protocol
		{SarlBsplProtocol.annotationInfo = current}
		modifiers+=CommonModifier*
		'protocol' name=ValidID '{' members+=BsplMember* '}'


//-----------------------------------------------
// START BSPL SUPPORT

BsplMember returns xtend::XtendMember:
	{xtend::XtendMember} annotations+=XAnnotation*
	(
		{SarlBsplRole.annotationInfo = current}
		'role' names+=ValidID (',' names+=ValidID)*
	|
		{SarlBsplParameter.annotationInfo = current}
		('var'|'parameter') parameters+=BsplParameter (',' parameters+=BsplParameter)*
	|
		{SarlBsplParameter.annotationInfo = current}
		private?='private' ('var'|'parameter')? parameters+=BsplParameter (',' parameters+=BsplParameter)*
	|
		{SarlBsplInteraction.annotationInfo = current}
		from=ValidID '->' toModifiers+=BsplParameterModifier* to=ValidID ':' message=ValidID '[' arguments+=BsplArgument (',' arguments+=BsplArgument)* ']'
	|
		{SarlBsplProtocolReference.annotationInfo = current}
		name=JvmTypeReference ( '(' BsplProtocolReferenceArguments ')' )?
	)
;

BsplParameter:
	{SarlBsplParameter}
	modifiers+=BsplParameterModifier* name=ValidID modifiers+=BsplParameterModifier* (':' type=JvmTypeReference)? modifiers+=BsplParameterModifier*
;

BsplArgument:
	{SarlBsplArgument}
	modifiers+=BsplParameterModifier* name=ValidID modifiers+=BsplParameterModifier*
;

BsplProtocolReferenceArguments:
	{SarlBsplProtocolReferenceArgument}
	(
		( roles+=ValidID ( ',' roles+=ValidID )* )? '|' ( arguments+=BsplArgument ( ',' arguments+=BsplArgument )* )?
	|
	)
;

BsplParameterModifier:
	'in' | 'out' | 'nil' | 'any' | 'opt' | 'null' | 'key'
;

