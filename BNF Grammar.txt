<Type> ::= <CommonModifiers> protocol <ValidID> { <BsplMembers> }

<BsplMembers> ::= <BsplMember> <BsplMembers>
<BsplMembers> ::= 

<BsplMember> ::= role <ValidID> <ValidIDList>
<BsplMember> ::= var <BsplParameter> <BsplParameters>
<BsplMember> ::= parameter <BsplParameter> <BsplParameters>
<BsplMember> ::= private <PrivateParam>
<BsplMember> ::= <ValidID> -> <ValidID> : <ValidID> [ <BsplArgument> <BsplArguments> ]

<PrivateParam> ::= var <BsplParameter> <BsplParameters>
<PrivateParam> ::= parameter <BsplParameter> <BsplParameters>

<ValidIDList> ::= , <ValidID> <ValidIDList>
<ValidIDList> ::=

<BsplParameters> ::= , <BsplParameter> <BsplParameters>
<BsplParameters> ::=

<BsplParameter> ::= <BsplParameterModifiers> <ValidID> <BsplParameterModifiers> <BsplParameterType> <BsplParameterModifiers>

<BsplParameterType> ::= : <JvmTypeReference>
<BsplParameterType> ::=

<BsplArguments> ::= , <BsplArgument> <BsplArguments>
<BsplArguments> ::=

BsplArgument ::= <BsplParameterModifiers> <ValidID> <BsplParameterModifiers>

<BsplParameterModifiers> ::= <BsplParameterModifier> <BsplParameterModifiers>
<BsplParameterModifiers> ::=

<BsplParameterModifier> ::= in
<BsplParameterModifier> ::= out
<BsplParameterModifier> ::= nil
<BsplParameterModifier> ::= null
<BsplParameterModifier> ::= key

