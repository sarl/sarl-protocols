package io.sarl.api.protocol.examples.prescription.adapter.doctor

import io.sarl.api.core.Behaviors
import io.sarl.api.core.Logging
import io.sarl.api.protocol.examples.prescription.adapter.events.Prescribe
import io.sarl.api.protocol.examples.prescription.adapter.events.PrescribeEnabled
import io.sarl.api.protocol.examples.prescription.adapter.events.RxBound
import io.sarl.api.protocol.examples.prescription.adapter.events.SymptomsBound
import io.sarl.lang.core.EventSpace
import io.sarl.lang.core.util.MutableOptional

skill DoctorProtocolSkill implements DoctorProtocolCapacity {
	
	uses Behaviors, Logging

	val protocolSpace : EventSpace

	val symptoms : MutableOptional<String> = MutableOptional.empty
	val rx : MutableOptional<String> = MutableOptional.empty
	
	new (protocolSpace : EventSpace) {
		this.protocolSpace = protocolSpace
	}

	def setSymptoms(symptoms : String) {
		if(this.symptoms.isPresent) {
			throw new RuntimeException("Already bound")
		}
		this.symptoms.set(symptoms)
		wake(new SymptomsBound)
		wakeIfEnabled
	}

	def getSymptoms : String {
		this.symptoms.get
	}
	
	def setRx(rx : String) {
		if (this.rx.isPresent) {
			throw new RuntimeException("Already bound")
		}
		this.rx.set(rx)
		wake(new RxBound)
		emitIfReady
	}

	private def wakeIfEnabled {
		if (this.symptoms.isPresent) {
			wake(new PrescribeEnabled)
		}
	}
	
	private def emitIfReady {
		if (this.symptoms.isPresent && this.rx.isPresent) {
			var evt = new Prescribe(this.symptoms.get, this.rx.get)
			info("Emit PRESCRIBE")
			this.protocolSpace.emit(owner.ID, evt)
		}
	}
	
}
